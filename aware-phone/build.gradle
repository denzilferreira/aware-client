apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.aware.phone"
        versionCode version_code
        versionName version_readable
        targetSdkVersion 25
        minSdkVersion 10 //Android 2.3.3
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi'
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

repositories {
    maven { url 'https://jitpack.io' }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//    all*.exclude group: 'com.awareframework', module: 'aware-phone'
}

dependencies {
//    compile project(':aware-core')
    compile "com.google.android.gms:play-services-vision:$google_libs"
    compile "com.android.support:cardview-v7:$support_libs"
    compile "com.android.support:appcompat-v7:$support_libs"
    compile "com.android.support:design:$support_libs"
    compile "me.dm7.barcodescanner:zbar:1.8.4"

    compile "com.github.denzilferreira:com.aware.plugin.device_usage:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.google.activity_recognition:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.ambient_noise:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.google.fused_location:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.openweather:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.google.auth:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.studentlife.audio_final:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.cmu_esm:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.contacts_list:$aware_libs"
    compile "com.github.denzilferreira:com.aware.plugin.fitbit:$aware_libs"
}